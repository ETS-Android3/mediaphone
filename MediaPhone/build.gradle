apply plugin: 'com.android.application'

android {
	compileSdkVersion 30

	defaultConfig {
		manifestPlaceholders = [providerId: 'mediaphone']
		applicationId 'ac.robinson.' + manifestPlaceholders.providerId
		buildConfigField 'String', 'PROVIDER_ID', '"' + manifestPlaceholders.providerId + '"'

		targetSdkVersion 30
		minSdkVersion 14
		versionCode 48
		versionName '1.6.6'
		// versionNameSuffix = '-beta-1'
		resConfigs 'en', 'es', 'fr', 'nl', 'pt', 'pl', 'ru'
	}

	buildTypes {
		all {
			buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + Calendar.getInstance()
					.getTimeInMillis() + "L)"
		}

		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-mediaphone.txt'

			shrinkResources true
		}
	}

	// automatically regenerate fastlane metadata for new releases
	task generateMetadata(type: Exec) {
		doFirst {
			workingDir '../fastlane/'
			commandLine 'python3', 'xmltofastlane.py'
			args '--versionCode', "$project.android.defaultConfig.versionCode"
		}
	}
	tasks.whenTaskAdded { task ->
		if (task.name == 'assembleRelease') {
			task.finalizedBy generateMetadata
		}
	}
}

dependencies {
	implementation project(':MediaUtilities')

	implementation 'com.google.android.material:material:1.4.0' // for overall UI appearance
	implementation 'androidx.exifinterface:exifinterface:1.3.2' // for auto-selection of export resolution
	implementation 'androidx.core:core:1.6.0' // for FileProvider
	implementation 'androidx.documentfile:documentfile:1.0.1' // for Storage Access Framework
}
